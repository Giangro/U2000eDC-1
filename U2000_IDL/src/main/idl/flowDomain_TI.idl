#ifndef flowDomain_TI_idl
#define flowDomain_TI_idl

// ********************************
// *                              *
// *    flowDomain_TI.idl         *
// *                              *
// ********************************

// Include list
#include "globaldefs.idl"
#include "common.idl"
#include "transmissionParameters.idl"
#include "terminationPoint.idl"
#include "flowDomainFragment_TI.idl"
#include "topologicalLink.idl"
#include "subnetworkConnection.idl"

#pragma prefix "mtnm.tmforum.org"

  /** 
   * <a href=../../../supportingDocumentation/SD1-27_Overview_NML-EML_Interface.pdf >SD1-27 Overview of 
   * NML-EML interface</a>
   *
   * <p>This module defines the types that represent resources for 
   * provisioning connectionless connectivity accross the NMS-EMS 
   * interface, the flow domain (FD) and matrix flow
   * domain (MFD).
   * </p>
   *
   * <h5> Version 3.5. </h5>
   **/

module flowDomain_TI
{
  /**
   * <p> This attribute provides an indication to the NMS about the server layer
   * connectivity between the MFDs associated to an FD. It shall have one of the 
   * following values: <br>
   * CS_UNKNOWN: The connectivity state of the FD is not known by the EMS<br>
   * CS_FULLY_CONNECTED: all FD Edge CPTPs are reachable to each other<br>
   * CS_NOT_FULLY_CONNECTED: At least one FD Edge CPTP is not reachable by
   * another FD Edge CPTP 
   * </p>
   **/
   enum ConnectivityState_T
   {
     CS_UNKNOWN,
     CS_FULLY_CONNECTED,
     CS_NOT_FULLY_CONNECTED
   };
   
  /**
   * <p> This attribute shall identify (for a "connectivity-aware" EMS) 
   * the requested operation mode in case not all FPs have potential connectivity 
   * to one another. If the EMS is not connectivity-aware, this parameter is ignored.
   * It shall have one of the following values: <br>
   * CR_IGNORE: in case not all FPs have potential connectivity to one another, 
   * add all Flow Points regardless of potential connectivity (default value).
   * CR_REJECT: in case not all FPs have potential connectivity 
   * to one another, reject the request. <br>
   *    <br>
   * </p>
   **/
   enum ConnectivityRequirement_T
   {
     CR_IGNORE,
     CR_REJECT
   };

  /**
   * <p> This parameter shall identify the role in the flow domain of
   * the requested CPTPs.
   * It shall have one of the following values: <br>
   * CPTPR_FD_EDGE: Return Flow Domain Edge CPTPs. <br>
   * CPTPR_FD_INTERNAL: Return Flow Domain Internal CPTPs. <br>
   * CPTPR_ALL: Return all CPTPs. <br>
   * </p>
   **/
   enum CPTP_Role_T
   {
     CPTPR_FD_EDGE,
     CPTPR_FD_INTERNAL,
     CPTPR_ALL
   };

  /**
   * <p> This (read-only) attribute provides an indication of the type 
   * of the FD. It may have one of the following values: <br>
   * "FDT_SINGLETON": The FD contains at most one MFD.
   * "FDT_NETWORK": The FD may contain more than one MFD.
   * </p>
   **/
typedef string FDType_T; 

  /**
   * <p>A list or transmission parameter group names.</p>
   **/
   typedef sequence<string> ParameterGroupsList_T;

  /**
   * <p>A flow domain is associated with a set of flow domain edge CPTPs and
   * matrix flow domains. Connectivity can be created between flow points 
   * contained by flow domain edge CPTPs through flow domain fragments.<br>
   * The read/write attributes can be set by the NMS by either the common methods 
   * of the common::Common_I interface or by the createFlowDomain(), 
   * modifyFlowDomain(), associateMFDsWithFlowDomain(), 
   * deAssociateMFDsFromFlowDomain(), associateCPTPsWithFlowDomain(), 
   * deAssociateCPTPsFromFlowDomain()
   * methods of the FlowDomainMgr_I interface.
   * </p>
   *
   * globaldefs::NamingAttributes_T <b>name</b>:
   * <br>The name represents the name of the flow domain
   * which is assigned by the EMS upon creation. The EMS is responsible
   * for guaranteeing the uniqueness of the name within the context
   * of the FlowDomainMgr_I. See    * <a href=../../../supportingDocumentation/SD1-25_objectNaming.pdf >SD1-25 SD1-25 Object Naming</a>
 for the naming of flow domains. <br>
   *
   * string <b>userLabel</b>:
   * <br>The user label of the flow domain is NMS data. The EMS may choose to
   * display the value of the attribute to the EMS user, but this is not a 
   * requirement. <br>
   *
   * string <b>nativeEMSName</b>:
   * <br>The name represents how the flow domain is referred to on EMS displays.
   * Its aim is to provide a "nomenclature bridge" to aid relating information
   * presented on NMS displays to EMS displays (via GUI cut through).
   * The native name is defaulted to a NULL string. However, this could
   * be used by the EMS for its implementation dependent purpose.<br>
   *
   * string <b>owner</b>:
   * <br>The owner is provisionable by the NMS.<br>
   *
   * transmissionParameters::LayeredParameterList_T <b>transmissionParams</b>:
   * <br>A list of layer rates on which connectivity can be established in the
   * flow domain together with a set of connectionless technology parameters for 
   * each layer rate. Refer to chapter "Connectionless Technology Parameters" of the
   * supporting document <a href=../../../supportingDocumentation/SD1-16_LayeredParameters.pdf > SD1-16_LayeredParameters.pdf</a>
 
   * for details of the currently defined
   * connectionless parameters.<br>
   *
   * string <b>networkAccessDomain</b>:
   * <br> The Network Access Domain to which this flow domain has been assigned
   * to.<br>
   *
   * ConnectivityState_T <b>fDConnectivityState</b>:
   * <br>Indication about the server layer connectivity between matrix flow
   * domains associated to the flow domain.<br>
   * 
   * FDType_T <b>fdType</b>:
   * <br> This (read-only) attribute provides an indication of the type of the FD. 
   * <br>
   *
   * globaldefs::NVSList_T <b>additionalInfo</b>:
   * <br>This attribute allows the communication from the EMS to the NMS of  
   * additional information which is not explicitly modelled, e.g. vendor specific
   * parameters. <br>
   * <br>
   **/
   struct FlowDomain_T
   {
     globaldefs::NamingAttributes_T name;
     string userLabel;
     string nativeEMSName;
     string owner;
     transmissionParameters::LayeredParameterList_T transmissionParams;
     string networkAccessDomain;
     ConnectivityState_T fDConnectivityState;
     FDType_T fdType;
     globaldefs::NVSList_T additionalInfo;
   };

  /**
   * <p>Sequence of FlowDomain_T objects.</p>
   **/
   typedef sequence<FlowDomain_T> FDList_T;

  /**
   * <p>In order to allow the NMS to deal with a large number of 
   * flow domain objects, iterators are used.</p>
   **/
   interface FDIterator_I
   {
      boolean next_n(in  unsigned long   how_many,
                     out FDList_T fdList)
        raises (globaldefs::ProcessingFailureException);

      unsigned long getLength()
        raises (globaldefs::ProcessingFailureException);

      void destroy()
        raises (globaldefs::ProcessingFailureException);
   };

  /**
   * <p>The read/write attributes required for the creation of an
   * flow domain on the EMS are packed together in an FDCreateData
   * structure which the NMS will pass to the EMS at flow domain
   * creation time.</p>
   *
   * globaldefs::NamingAttributes_T <b>name</b>:
   * <br>The identifier of the flow domain which will be used over the interface.
   * The EMS has to make sure that the name of the flow domain is unique within the
   * EMS. If no name is provided by the NMS, the EMS has to define a
   * unique name.<br>
   *
   * string <b>userLabel</b>:
   * <br>An userLabel may be specified by the NMS. May be empty.<br>
   *
   * boolean <b>forceUniqueness</b>:
   * <br>Specifies whether uniqueness of the userLabel is required amongst flow
   * domains of the EMS. In case of uniqueness, the operation will fail if the
   * userLabel is already in use.<br>
   *
   * string <b>owner</b>:
   * <br>An owner may be specified by the NMS. May be empty.<br>
   *
   * string <b>networkAccessDomain</b>:
   * <br> The Network Access Domain to which the flow domain shall be assigned
   * to.<br>
   *
   * globaldefs::NamingAttributesList_T <b>mfds</b>:
   * <br>Identifies the list of existing matrix flow domains to be associated to
   * the flow domain. The MFDs must not already be associated to another flow domain.
   * This may be an empty list.<br>
   *
   * transmissionParameters::LayeredParameterList_T <b>transmissionParams</b>:
   * <br>A list of layer rates on which connectivity can be established in the
   * flow domain together with a set of connectionless parameters for each layer
   * rate. Refer to chapter "Connectionless Technology Parameters" of the supporting
   * document <a href=../../../supportingDocumentation/SD1-16_LayeredParameters.pdf > SD1-16_LayeredParameters.pdf</a> 
   * for details of the currently defined connectionless
   * parameters.<br>
   *
   * globaldefs::NVSList_T <b>additionalCreationInfo</b>:
   * <br>Additional creation information may be specified by the NMS. This may
   * be an empty list.<br>
   *<br>
   **/
   struct FDCreateData_T
   {
     globaldefs::NamingAttributes_T name;
     string userLabel;
     boolean forceUniqueness;
     string owner;
     string networkAccessDomain;
     globaldefs::NamingAttributesList_T mfds;
     transmissionParameters::LayeredParameterList_T transmissionParams;
     globaldefs::NVSList_T additionalCreationInfo;
   };

  /**
   * <p>The read/write attributes required for the modification of a
   * flow domain on the EMS are packed together in an FDModifyData
   * structure which the NMS will pass to the EMS in a request to modify an 
   * existing flow domain.</p>
   *
   * string <b>userLabel</b>:
   * <br>An userLabel may be specified by the NMS. May be empty.<br>
   *
   * boolean <b>forceUniqueness</b>:
   * <br>Specifies whether uniqueness of the userLabel is required amongst
   * flow domains of the EMS. In case of uniqueness, the operation will fail
   * if the userLabel is already in use.<br>
   *
   * string <b>owner</b>:
   * <br>An owner may be specified by the NMS. May be empty.<br>
   *
   * string <b>networkAccessDomain</b>:
   * <br> The Network Access Domain to which the flow domain shall be assigned
   * to.<br>
   *
   * transmissionParameters::LayeredParameterList_T <b>transmissionParams</b>:
   * <br>This list specifies the connectionless parameters that shall be modified.
   * As an input only the list of parameters to be changed, removed, or added
   * shall be provided. If an entry is to be removed, "-" must be specified as 
   * a value. Refer to chapter "Connectionless Technology Parameters" of the supporting
   * document <a href=../../../supportingDocumentation/SD1-16_LayeredParameters.pdf > SD1-16_LayeredParameters.pdf</a> for details of the currently defined connectionless
   * parameters.<br>
   *
   * globaldefs::NVSList_T <b>additionalModificationInfo</b>:
   * <br>Additional modification information may be specified by the NMS.<br>
   *<br>
   **/
   struct FDModifyData_T
   {
     string userLabel;
     boolean forceUniqueness;
     string owner;
     string networkAccessDomain;
     transmissionParameters::LayeredParameterList_T transmissionParams;
     globaldefs::NVSList_T additionalModificationInfo;
   };

  
  /**
   * <p>The read/write attributes required for the creation of a
   * floating termination point on the EMS are packed together in an 
   * FTPCreateData structure which the NMS will pass to the EMS at 
   * FTP creation time.</p>
   *
   * <p>The FTP is created in unconnected state (i.e., 
   * the connectionstate attribute has the value TPCS_NOT_CONNECTED;
   * see terminationPoint::TPConnectionState_T). </p>
   *
   * globaldefs::NamingAttributes_T <b>equipmentName</b>:
   * <br>The identifier of the equipment hosting the new FTP in the NE.
   * <br>
   *
   * string <b>userLabel</b>:
   * <br>An userLabel may be specified by the NMS. May be empty.<br>
   *
   * boolean <b>forceUniqueness</b>:
   * <br>Specifies whether uniqueness of the userLabel is required amongst
   * FTPs of the EMS. In case of uniqueness, the operation will fail if the
   * userLabel is already in use.<br>
   *
   * string <b>owner</b>:
   * <br>An owner may be specified by the NMS. May be empty.<br>
   *
   * string <b>networkAccessDomain</b>:
   * <br> The Network Access Domain to which this FTP
   * has been assigned to.<br>
   *
   * globaldefs::NamingAttributes_T <b>ingressTransmissionDescriptorName</b>:
   * <br>The FTP may have an optional reference to an 
   * ingress (incoming) Transmission Descriptor. <br>
   *
   * globaldefs::NamingAttributes_T <b>egressTransmissionDescriptorName</b>:
   * <br>The FTP may have an optional reference to an 
   * egress (outgoing) Transmission Descriptor.  <br>
   *
   *
   * TerminationMode_T <b>tpMappingMode</b>:
   * <br>Within the façade definition, the FTP can act as an 
   * aggregate of associated G.805 TCPs, G.805 Termination Functions and G.805 
   * CPs at one or more LayerRates. 
   *
   * The tpMappingMode attribute indicates and controls the connection of
   * the named CP at a specified LayerRate to the dedicated G.805 TCP and 
   * associated G.805 Termination Function at the same LayerRate within the 
   * FTP.
   *
   * The TerminationMode is used, for example, to control the Termination and
   * mapping to VT1.5 of an STS1 Trail within an OC3 port. This capability
   * could potentially be used to terminate an STS1 backbone Trail within 
   * a lower order mux that has the capability to alternatively 
   * cross-connect the STS1 unterminated. The same capability is used in SDH
   * and is potentially applicable to any LayerRate.<br>
   *
   * Directionality_T <b>direction</b>:
   * <br>The direction of the termination point. 
   * It is a readonly attribute.<br>
   *
   * transmissionParameters::LayeredParameterList_T <b>transmissionParams</b>:
   * <br>A list of layer rates on which connectivity can be established in the
   * flow domain together with a set of connectionless parameters for each layer
   * rate. Refer to chapter "Connectionless Technology Parameters" of the supporting
   * document <a href=../../../supportingDocumentation/SD1-16_LayeredParameters.pdf > SD1-16_LayeredParameters.pdf</a> for details of the currently defined connectionless
   * parameters.<br>
   *
   * globaldefs::NVSList_T <b>additionalCreationInfo</b>:
   * <br>Additional creation information may be specified by the NMS. This may
   * be an empty list.<br>
   *
   * <p>The following TP attributes are fixed, and therefore do not appear in structure:</p>
   *
   * TPType_T <b>type</b>:
   * <br> Fixed value TPT_PTP (FTPs use the same 
   * type as PTPs for compatibility reasons.)
   * <br>
   *
   * <p>The following TP attributes will have their value determined by the EMS
   * at FTP creation time, and therefore do not appear in the structure:</p>
   *
   * string <b>nativeEMSName</b>:
   * <br>The name represents how the TP is refered to on EMS displays. Its
   * aim is to provide a "nomenclature bridge" the aid relating information
   * presented on NMS displays to EMS displays (via GUI cut through).
   * It is never set to NULL string.<br>
   *
   *<br>
   **/

   struct FTPCreateData_T
   {
     globaldefs::NamingAttributes_T equipmentName;
     string userLabel;
     boolean forceUniqueness;
     string owner;
     string networkAccessDomain;
     globaldefs::NamingAttributes_T ingressTransmissionDescriptorName;
     globaldefs::NamingAttributes_T egressTransmissionDescriptorName;
     terminationPoint::TerminationMode_T tpMappingMode;
     terminationPoint::Directionality_T direction;
     transmissionParameters::LayeredParameterList_T transmissionParams;
     globaldefs::NVSList_T additionalCreationInfo;
   };

  
  /**
   * <p>The FlowDomainMgr_I is used to gain access to flow domains,
   * matrix flow domains, flow domain fragments and their operations.</p>
   *
   * <p>A handle to an instance of this interface is gained via the
   * emsSession::EmsSession_I::getManager() operation.</p>
   **/

  interface FlowDomainMgr_I : common::Common_I
  {
  
  
  /**
   * <p>This allows an NMS to request a list of flow domain fragments for the
   * specified flow domain at the specified connectivityRates.
   * </p>
   *
   * <p>In order to allow the NMS to deal with a large number of objects,
   * this operation uses an iterator.</p>
   *
   * globaldefs::NamingAttributes_T <b>fdName</b>: 
   * <br>Name of the flow domain.<br>
   *
   * transmissionParameters::LayerRateList_T <b>connectivityRateList</b>:
   * <br>List of rates of the flow domain fragments to be reported.
   * If an empty list is specified, then all flow domain fragments of all 
   * rates are to be reported.<br>
   *
   * unsigned long <b>how_many</b>:
   * <br>Maximum number of flow domai fragments to be reported in the first
   * batch.<br>
   *
   * flowDomainFragment_TI::FDFrList_T <b>fdfrList</b>:
   * <br>First batch of flow domain fragments.<br>
   *
   * flowDomainFragment_TI::FDFrIterator_I <b>fdfrIt</b>:
   * <br>Iterator to retrieve the remaining flow domain fragment.<br>
   *
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when fdName does not reference a
   *  flow domain object or connectivityRateList contains undefined 
   *  values<br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when fdName references an object that 
   *  does not exist<br>
   * EXCPT_TOO_MANY_OPEN_ITERATORS - Raised when maximum number of iterators 
   *  that the EMS can support has been reached<br>
   * EXCPT_NE_COMM_LOSS - Raised when communcation is lost to a managed element
   *  involved in this operation<br>
   * <br>
   **/
   void getAllFDFrs(
         in globaldefs::NamingAttributes_T fdName,
         in unsigned long how_many,
         in transmissionParameters::LayerRateList_T connectivityRateList,
         out flowDomainFragment_TI::FDFrList_T fdfrList,
         out flowDomainFragment_TI::FDFrIterator_I fdfrIt) 
        raises(globaldefs::ProcessingFailureException);


  
  /**
   * <p>This operation returns the flow domain fragment structure 
   * for the flow domain fragment whose name is supplied as a parameter.</p>
   *
   * globaldefs::NamingAttributes_T <b>fdfrName</b>:
   * <br>The name of the flow domain fragment to retrieve.<br>
   *
   * flowDomainFragment_TI::flowDomainFragment_T <b>fdfr</b>:
   * <br>The flow domain fragment structure retrieved.<br>
   *
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   * failure<br>
   * EXCPT_INVALID_INPUT - Raised when fdfrName does not reference a 
   *  flow domain fragment object<br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when fdfrName references a flow domain
   *  fragment object that does not exist<br>
   * EXCPT_NE_COMM_LOSS - Raised when communication is lost to a managed element
   *  involved in this operation<br>
   * <br>
   **/
   void getFDFr(
         in globaldefs::NamingAttributes_T fdfrName,
         out flowDomainFragment_TI::FlowDomainFragment_T fdfr)
        raises (globaldefs::ProcessingFailureException);

  /**
   * <p>The NMS invokes the createAndActivateFDFr service to request the EMS to create
   * and activate a flow domain fragment given the parameters passed in the method.</p>
   *
   * flowDomainFragment_TI::FDFrCreateData_T <b>createData</b>:
   * <br>Structure describing the FDFr structure to be created. Setting of
   * connectionless parameters is best effort (except where specified otherwise 
   * for a particular parameter).<br>
   *
   * ConnectivityRequirement_T <b>connectivityRequirement</b>:
   * <br> For a "connectivity-aware" EMS, this parameter shall identify 
   * the requested operation mode in case not all FPs have potential connectivity 
   * to one another. If the EMS is not connectivity-aware, this parameter is ignored.
   * <br>
   *
   * globaldefs::NamingAttributesList_T <b>aEnd</b>:
   * <br> A list of of CPTP names that delimit the FDFr and characterize 
   * its edges (entrance and/or exit points). As a result of creating the FDFr,
   * FPs are created as clients of the FD Edge CPTPs. 
   * <br> In case of a unidirectional 
   * FDFr this attribute contains the list of <b>source</b> FD Edge CPTPs. 
   * <br> In case of a 
   * bidirectional FDFr this attribute may be combined with the zEnd TPs 
   * attribute to obtain all the FD Edge CPTPs that are associated to the 
   * FDFr. 
   * <br> (N.B. For a bidirectional point to point FDFr it is suggested, but not 
   * mandatory, to put one TP in the aEnd and one in the zEnd, as with SNCs and 
   * TLs. For a multipoint FDFr, or a point-to-point FDFr that may be expanded 
   * to multipoint, it is suggested to put all the TPs in the aEnd.) 
   * <br>
   *
   * globaldefs::NamingAttributesList_T <b>zEnd</b>:
   * <br> In case of a unidirectional FDFr this attribute contains the list of 
   * <b>sink</b> FD Edge CPTPs that delimit the FDFr and characterize its edges
   *  (exit points). 
   * <br> As a result of creating the FDFr, FPs are created as 
   * clients of the FD Edge CPTPs. 
   * <br> In case of a bidirectional FDFr this attribute 
   * may be combined with the aEnd TPs attribute to obtain all the FD Edge CPTPs 
   * that are associated to the FDFr.
   * <br>
   *
   * globaldefs::NamingAttributesList_T <b>internalTPs</b>:
   * <br>A (possibly empty) list of internal CPTP names that must be included 
   * in the route of the FDFr. As a result of creating the FDFr, 
   * FPs are created as clients of the internal CPTPs.
   * <br>
   *
   * flowDomainFragment_TI::MatrixflowDomainFragment_TIList_T <b>mfdfrs</b>:
   * <br> An optional (possibly empty) list of MFDFrs that make up the route 
   * of the FDFr. This attribute may be omitted if the FDFr is routed by the network. 
   * As a result of creating the FDFr, MFDFrs are created in the various MFDs.
   * <br>
   *
   * subnetworkConnection::TPDataList_T <b>tpsToModify</b>: 
   * <br>A list of TPs and parameters to apply. On method return the list is 
   * updated to provide the resulting parameters. The list may refer to
   * flow points that are being created during the createAndActivateFDFr request
   * or to the containing CPTPs. <br>
   * 
   * flowDomainFragment_TI::flowDomainFragment_T <b>theFDFr</b>:
   * <br>The new created and activated flow domain fragment. The EMS is responsible
   * for guaranteeing uniqueness of the name of the flow domain fragment. <br>
   *
   * globaldefs::NamingAttributesList_T <b>notConnectableCPTPList</b>:
   * <br>The list of Connectionless Port Termination Points that could not 
   * be connected. <br> 
   *
   * globaldefs::NamingAttributesList_T <b>parameterProblemsTPList</b>:
   * <br>The list of Connectionless Port Termination Points and Flow Points FPs
   * for which only best-effort transmission parameters could not be set. <br>
   *
   * string <b>errorReason</b>:
   * <br>In case a "best effort" parameter could not be set or a flow point could not
   * be connected an indication of the fault reason is provided by the EMS.<br>
   *
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when any input parameter is syntactically 
   *  incorrect (e.g. field of createData is invalid).<br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when one of the provided CPTPs does not exist.
   * <br>
   * EXCPT_USERLABEL_IN_USE - Raised when the userLabel uniqueness constraint is 
   *  not met; the specified user label is currently being used.<br>
   * EXCPT_UNABLE_TO_COMPLY - Raised when the EMS is unable 
   *  to execute the request because of one of the following conditions:<ul> 
   * <li> at least one of the parameters although 
   *  valid cannot be set and that parameter is identified as 
   *  "not best effort"; </li>
   *  <li> the name specified by the NMS exists already in the EMS;</li>
   *  <li> The FP total connectivity constraint is not met;</li>
   * <li> Unrecognized mapping criteria;</li>
   * <li> Frames map to more than one FDFr;</li>
   * <li> The FDFr would have less than two FPs.</li>
   * </ul>  <br>
   * EXCPT_NE_COMM_LOSS - Raised in case of Communications failure between the EMS 
   * and the ME(s). 
   * <br>
   * EXCPT_NOT_IN_VALID_STATE - Raised when the state of the specified object is such 
   * that the EMS cannot perform the request.. 
   * <br>
   * EXCPT_TP_INVALID_ENDPOINT - Raised when the specified TP does not exist or cannot be created. <br>
   * <br>
   **/
   void createAndActivateFDFr(
         in flowDomainFragment_TI::FDFrCreateData_T createData,
         in ConnectivityRequirement_T connectivityRequirement,
         in globaldefs::NamingAttributesList_T aEnd,
         in globaldefs::NamingAttributesList_T zEnd,
         inout globaldefs::NamingAttributesList_T internalTPs,
         inout flowDomainFragment_TI::MatrixFlowDomainFragmentList_T mfdfrs,
         inout subnetworkConnection::TPDataList_T tpsToModify,
         out flowDomainFragment_TI::FlowDomainFragment_T theFDFr, 
         out globaldefs::NamingAttributesList_T notConnectableCPTPList,
         out globaldefs::NamingAttributesList_T parameterProblemsTPList,
         out string errorReason)
        raises (globaldefs::ProcessingFailureException);

  /**
   *<p>This service allows an NMS to request the deactivation and deletion of a flow
   * domain fragment from a flow domain.</p>
   *
   * <br>NamingAttributes_T <b>fdfrName</b>:
   * <br>The name of the flow domain fragment to be deactivated and deleted.<br>
   *
   * subnetworkConnection::TPDataList_T <b>tpsToModify</b>: 
   * <br>A list of TPs and parameters to apply. On method return the list is 
   * updated to provide the resulting parameters. The list may refer only to
   * TPs that take part in the flow domain fragment deletion process. 
   * <br>
   *
   * string <b>errorReason</b>:
   * <br>Specifies the fault reason if any.<br>
   *
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when any input parameter is syntactical 
   *  incorrect (e.g. fdfrName does not refer to an FDFr object)<br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when fdfrName references an object that does 
   *  not exist<br>
   * EXCPT_ACCESS_DENIED - Raised if the EMS does not allow the NMS to delete the
   *  flow domain fragment, e.g. if the flow domain fragment is fixed <br>
   * EXCPT_NE_COMM_LOSS - Raised when communcation is lost to a managed element
   *  involved in this operation<br>
   * <br>
   **/
   void deactivateAndDeleteFDFr(
         in globaldefs::NamingAttributes_T fdfrName,
         inout subnetworkConnection::TPDataList_T tpsToModify,
         out flowDomainFragment_TI::FlowDomainFragment_T theFDFr,
         out string errorReason)
        raises (globaldefs::ProcessingFailureException);

  /**
   * <p>The NMS invokes the modifyFDFr service to request the EMS to modify an 
   * existing flow domain fragment as specified by the parameters passed in the
   * method.</p>
   *
   * globaldefs::NamingAttributes_T <b>fdfrName</b>:
   * <br>The name of the flow domain fragment to modify.<br>
   *
   * flowDomainFragment_TI::FDFrModifyData_T <b>fdfrModifyData</b>:
   * <br>Structure describing how the flow domain fragment should be modified.
   * Modification of connectionless parameters is best effort (except where 
   * specified otherwise for a particular parameter).<br>
   *
   * ConnectivityRequirement_T <b>connectivityRequirement</b>:
   * <br> For a "connectivity-aware" EMS, this parameter shall identify 
   * the requested operation mode in case not all FPs have potential connectivity 
   * to one another. 
   * <br> This parameter is only significant if the fdfrModifyData parameter
   * contains flow points to be added or removed, otherwise it shall be ignored
   * by the EMS. If the EMS is not connectivity-aware, this parameter is also ignored.
   * <br>
   *
   * subnetworkConnection::TPDataList_T <b>tpsToModify</b>: 
   * <br>A list of TPs and parameters to apply. On method return the list is 
   * updated to provide the resulting parameters. The list may refer to
   * flow points or to the containing CPTPs. <br>
   *
   * globaldefs::NamingAttributesList_T <b>failedTPList</b>:
   * <br> The list of Connectionless Port Termination Points 
   * (requested in the fdfrModifyData parameter) 
   * that could not be added or could not be removed, 
   * whichever was requested.<br>
   *
   * globaldefs::NamingAttributesList_T <b>parameterProblemsTPList</b>:
   * <br> The list of Connectionless Port Termination Points and Flow Points FPs
   *  for which only best-effort transmission parameters could not be set. <br>
   *
   * flowDomainFragment_TI::flowDomainFragment_T <b>newFDFr</b>:
   * <br>The modified flow domain fragment.<br>
   *
   * string <b>errorReason</b>:
   * <br>In case a "best effort" parameter could not be set an indication of the 
   * fault reason is provided by the EMS.<br>
   *
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when any input parameter is syntactically 
   *  incorrect (e.g. field of fdfrModifyData is invalid).<br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when fdfrName references an object that does 
   *  not exist<br>
   * EXCPT_UNABLE_TO_COMPLY - Raised when the EMS is unable 
   *  to execute the request because at least one of the parameters although 
   *  valid cannot be set and that parameter is identified as 
   *  "not best effort" or if the EMS cannot meet the GradesOfImpact requested by
   *  the NMS <br>
   * EXCPT_USERLABEL_IN_USE - Raised when the userLabel uniqueness constraint is 
   *  not met<br>
   * EXCPT_ACCESS_DENIED - Raised if the EMS does not allow the NMS to modify the
   *  flow domain fragment, e.g. if the flow domain fragment is fixed <br>
   * EXCPT_NOT_IN_VALID_STATE - Raised when at least one of the specified CPTPs
   *  is in the "unassigned" "Port TP role state"<br>
   * <br>
   **/

   void modifyFDFr(
         in globaldefs::NamingAttributes_T fdfrName,
         in flowDomainFragment_TI::FDFrModifyData_T fdfrModifyData,
         in ConnectivityRequirement_T connectivityRequirement,
         inout subnetworkConnection::TPDataList_T tpsToModify,
         out globaldefs::NamingAttributesList_T failedTPList,
         out globaldefs::NamingAttributesList_T parameterProblemsTPList,
         out flowDomainFragment_TI::FlowDomainFragment_T newFDFr,
         out string errorReason)
        raises (globaldefs::ProcessingFailureException);
        
   /**
   * <p>Using this service the NMS requests from the EMS the creation of a
   * FTP given the parameters passed in the method. 
   * </p>
   *
   * FTPCreateData_T <b>createData</b>:
   * <br>Structure describing the floating termination point to be created.<br>
   *
   * TPDataList_T <b>tpsToModify</b>: A list of TPs and parameters to apply, 
   * updated to provide the resulting parameters. 
   *
   * <br> Applying transmission parameters is best-effort (where not stated 
   * otherwise) and the resulting values of the transmission parameters are 
   * provided in the updated tpsToModify parameter. If the parameter is "not 
   * best effort" then the whole operation will be rejected and the appropriate 
   * exception thrown (see exceptions below).</p>
   *
   * <br> This parameter may be used by the NMS to determine
   * the number of server layer CTPs to create for the specified FTP.
   * An empty list means that the number of server layer CTPs 

   * is determined by the EMS. <br>
   * 
   *
   * TerminationPoint_T <b>theFTP</b>:
   * <br>The new floating termination point. The EMS is responsible for guaranteeing
   * uniqueness of the name of the FTP. The name may be specified
   * by the NMS in the createData. <br>
   *
   * string <b>errorReason</b>:
   * <br> Specifies the creation error(s) if any.
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when any input parameter is syntactically 

   *  incorrect (e.g. field of createData is invalid).<br>
   * EXCPT_UNABLE_TO_COMPLY - Raised when the EMS (or NE) does not support explicit creation 
   * of CPTPs. <br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when the specified equipment does not exist.
   * <br>
   * EXCPT_CAPACITY_EXCEEDED - Raised when The Equipment does not have the resources 
   * (e.g., not enough overall back-plane bandwidth, not enough usable timeslots, etc.)<br>
   * EXCPT_NE_COMM_LOSS - Raised when communcation is lost to the managed element
   *  that shall contain the FTP<br>
   * EXCPT_USERLABEL_IN_USE - Raised when the userLabel uniqueness constraint is 
   *  not met<br>
   * EXCPT_OBJECT_IN_USE - Raised when the object identified in the request is
   *  currently in use<br>
   * <br>
   **/  
        
   void createFTP(
         in FTPCreateData_T createData,
         inout subnetworkConnection::TPDataList_T tpsToModify,
         out terminationPoint::TerminationPoint_T theFTP,
         out string errorReason)
        raises (globaldefs::ProcessingFailureException);

  /**
   * <p>This service allows an NMS to request the deletion of a floating termination point.</p>
   *
   * NamingAttributes_T <b>tpName</b>:
   * <br>The name of the termination point to be deleted.<br>
   *
   * TPDataList_T <b>tpsToModify</b>: in: The list of TPs with associated 
   * parameters to be applied. out: The list of TPs with associated 
   * applied parameters. <br>
   *
   * string <b>errorReason</b>:
   * <br>In case the operation completes without exception, 
   * but some error occurred or part of the request could not be filled,
   * an indication of the fault reason
   * is provided by the EMS.<br>
   * <b>Raises</b> globaldefs::ProcessingFailureException <br>
   * EXCPT_NOT_IMPLEMENTED - Raised if the entire request is not supported 
   * by the EMS or the request with the specified input parameters 
   * is not supported. <br>
   * EXCPT_INTERNAL_ERROR - Raised in case of non-specific EMS internal 
   *  failure<br>
   * EXCPT_INVALID_INPUT - Raised when any input parameter is syntactical 
   *  incorrect, i.e. tpName does not refer to a "CPTP" termination point object <br>
   * EXCPT_ENTITY_NOT_FOUND - Raised when tpName references an object that does 
   *  not exist <br>
   * EXCPT_OBJECT_IN_USE - Raised when the CPTP is an ELL endpoint.
   *  <br>
   * EXCPT_UNABLE_TO_COMPLY - Raised if the TP is is not deleteable.
   *  <br>  
   * EXCPT_NE_COMM_LOSS - Raised when communcation is lost to a managed element
   *  involved in this operation<br>
   * <br>
   **/
   void deleteFTP(
         in globaldefs::NamingAttributes_T ftpName,
         inout subnetworkConnection::TPDataList_T tpsToModify,
         out string errorReason)
        raises (globaldefs::ProcessingFailureException);
};
  

};

#endif // flowDomain_TI_idl
